// Copyright (c) 2018 WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
//
// WSO2 Inc. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.
import ballerina/log;

public const string TEAM = "TEAM";
public const string OWNER = "OWNER";
public const string MAINTAINER = "MAINTAINER";

# Pre-defined labels for cellery components.
public type Label "TEAM"|"OWNER"|"MAINTAINER";

public type DockerSource record {
    string Dockerfile;
    string tag;
    !...;
};

public type ImageSource record {
    string image;
    !...;
};

public type GitSource record {
    string gitRepo;
    string tag;
    !...;
};

public type ApiDefinition record {
    string path;
    string method;
    !...;
};

public type API record {
    string targetComponent;
    boolean global;
    HttpApiIngress ingress;
    !...;
};

public type TCP record{
    string targetComponent;
    TCPIngress ingress;
};

public type GRPC record{
    string targetComponent;
    TCPIngress ingress;
};

public type Web record{
    string targetComponent;
    WebProperties properties;
    WebIngress ingress;
};

public type Resiliency record {
    RetryConfig retryConfig?;
    FailoverConfig failoverConfig?;
    !...;
};

public type RetryConfig record {
    int interval;
    int count;
    float backOffFactor;
    int maxWaitInterval;
    !...;
};

public type FailoverConfig record {
    int timeOut;
    !...;
};

public type AutoScaling record {
    AutoScalingPolicy policy;
    boolean overridable = true;
    !...;
};

public type AutoScalingPolicy record {
    int minReplicas;
    int maxReplicas;
    CpuUtilizationPercentage cpuPercentage;
    !...;
};

public type CpuUtilizationPercentage object {
    public int percentage;
    public function __init(int percentage) {
        self.percentage = percentage;
    }
};

public type Component record {
    string name;
    ImageSource source;
    int replicas = 1;
    map<TCPIngress|HttpApiIngress|GRPCIngress|WebIngress> ingresses?;
    map<string> labels?;
    map<ParamValue> parameters?;
    AutoScaling autoscaling?;
    !...;
};

public type TCPIngress object {
    public int port;
    public int targetPort;
    public function __init(int port, int targetPort) {
        self.port = port;
        self.targetPort = targetPort;
    }
};

public type GRPCIngress object {
    public int port;
    public int targetPort;
    public string protoFile;
    public function __init(int port, int targetPort, string protoFile = "") {
        self.port = port;
        self.targetPort = targetPort;
        self.protoFile = protoFile;
    }
};

public type HttpApiIngress object {
    public int port;
    public string context;
    public ApiDefinition[] definitions;

    public function __init(int port, string context, ApiDefinition[] definitions) {
        self.port = port;
        self.context = context;
        self.definitions = definitions;
    }
};

public type WebIngress object {
    public int port;
    public string uri;

    public function __init(int port, string uri = "/") {
        self.port = port;
        self.uri = uri;
    }
};

public const string TLS_REQUIRED = "REQUIRED";
public const string TLS_NONE = "NONE";

public type TLS "REQUIRED"|"NONE";

public type HttpProperties record {
    boolean requireAuthentication = false;
};

public type WebProperties record {
    string vhost;
    string context = "/";
    TLS tls = TLS_NONE;
    boolean requireAuthentication = false;
};

public type ParamValue abstract object {
    public string|int|boolean|float? value;
};

public type Env object {
    *ParamValue;
    public function __init(string|int|boolean|float? default = ()) {
        self.value = default;
    }
};

public type Secret object {
    *ParamValue;
    public string path;

    public function __init() {
        self.path = "";
        self.value = "";
    }
};

public const string GATEWAY_ENVOY = "Envoy";
public const string GATEWAY_MICRO_GATEWAY = "MicroGateway";

public type GatewayType "Envoy"|"MicroGateway";

public type CellImage object {
    GatewayType gatewayType = GATEWAY_ENVOY;
    string hostname = "";
    public map<Component> components = {};
    public map<API> apis = {};
    public map<TCP> tcp = {};
    public map<GRPC> grpc = {};
    public map<Web> web = {};

    public function addComponent(Component component) {
        self.components[component.name] = component;
    }

    # Expose the all the ingresses in a component via Cell Gateway
    #
    # + component - The component record
    # + properties - HTTP configurations
    public function exposeLocal(Component component, HttpProperties? properties = ()) {
        foreach var (name, ingressTemp) in component.ingresses {
            if (ingressTemp is HttpApiIngress) {
                self.apis[name] = {
                    targetComponent: component.name,
                    ingress: ingressTemp,
                    global: false
                };
            } else if (ingressTemp is GRPCIngress){
                self.grpc[name] = {
                    targetComponent: component.name,
                    ingress: ingressTemp
                };
            }
            else if (ingressTemp is TCPIngress){
                self.tcp[name] = {
                    targetComponent: component.name,
                    ingress: ingressTemp
                };
            }
        }
    }

    # Expose a given ingress via Cell Gateway
    #
    # + component - The component record
    # + ingressName - Name of the ingress to be exposed
    # + properties - HTTP configurations
    public function exposeIngressLocal(Component component, string ingressName, HttpProperties? properties
        = ()) {
        TCPIngress|HttpApiIngress|GRPCIngress|WebIngress? ingress = component.ingresses[ingressName];
        if (ingress is HttpApiIngress) {
            self.apis[ingressName] = {
                targetComponent: component.name,
                ingress: ingress,
                global: false
            };
        } else if (ingress is GRPCIngress){
            self.grpc[ingressName] = {
                targetComponent: component.name,
                ingress: ingress
            };
        } else if (ingress is TCPIngress){
            self.tcp[ingressName] = {
                targetComponent: component.name,
                ingress: ingress
            };
        }
        else {
            error err = error("Ingress " + ingressName + " not found in the component " + component.name + ".");
            panic err;
        }
    }

    # Expose the all the HttpApiIngress ingresses in a component via Global Gateway
    #
    # + component - The component record
    # + properties - HTTP/Web configurations
    public function exposeGlobal(Component component, WebProperties|HttpProperties? properties = ()) {
        foreach var (name, ingressTemp) in component.ingresses {
            if (ingressTemp is HttpApiIngress) {
                //TODO: Handle HTTP properties.
                self.apis[name] = {
                    targetComponent: component.name,
                    ingress: ingressTemp,
                    global: true
                };
            } else if (ingressTemp is WebIngress){
                if (!(properties is WebProperties)) {
                    error err = error("Unable to expose ingress " + name + ". Requried WebProperties not provided.");
                    panic err;
                }
                self.web[name] = {
                    targetComponent: component.name,
                    ingress: ingressTemp,
                    properties: <WebProperties>properties,
                    global: true
                };
            } else {
                error err = error("Unable to expose ingress " + name + ". Unsupported ingress type.");
                panic err;
            }
        }
    }

    # Expose a given ingress via Global Cell Gateway
    #
    # + component - The component record
    # + ingressName - Name of the ingress to be exposed
    # + properties - HTTP/Web configurations
    public function exposeIngressGlobal(Component component, string ingressName, WebProperties|HttpProperties?
        properties = ()) {
        TCPIngress|HttpApiIngress|GRPCIngress|WebIngress? ingress = component.ingresses[ingressName];
        if (ingress is HttpApiIngress) {
            self.apis[ingressName] = {
                targetComponent: component.name,
                ingress: ingress,
                global: true
            };
        } else if (ingress is GRPCIngress){
            self.grpc[ingressName] = {
                targetComponent: component.name,
                ingress: ingress
            };
        }
        else if (ingress is TCPIngress){
            self.tcp[ingressName] = {
                targetComponent: component.name,
                ingress: ingress
            };
        }
        else if (ingress is WebIngress){
            if ((properties is WebProperties)) {
                self.hostname = properties.vhost;
                self.web[ingressName] = {
                    targetComponent: component.name,
                    ingress: ingress,
                    properties: <WebProperties>properties,
                    global: true
                };
            } else {
                error err = error("Unable to expose ingress " + ingressName + ". Requried WebProperties not provided.");
                panic err;

            }
        }
        else {
            error err = error("Unable to expose ingress " + ingressName + ". Unsupported ingress type.");
            panic err;
        }
    }

};

# Build the cell aritifacts
#
# + cellImage - The cell image definition
# + orgName - The cell  image org
# + imageName - The cell image name
# + imageVersion - The cell image version
# + return - true/false
public extern function createImage(CellImage cellImage, string orgName,
                                   string imageName, string imageVersion) returns (boolean|error);

# Update the cell aritifacts with runtime changes
#
# + cellImage - The cell image definition
# + imageName - The cell image name
# + imageVersion - The cell image version
# + instanceName - The cell instance name
# + return - true/false
public extern function createInstance(CellImage cellImage, string imageName,
                                      string imageVersion, string instanceName) returns (boolean|error);

# Parse the swagger file and returns API Defintions
#
# + swaggerFilePath - The swaggerFilePath
# + return - Array of ApiDefinitions
public extern function readSwaggerFile(string swaggerFilePath) returns (ApiDefinition[]);

public function getHost(string cellImageName, Component component) returns (string) {
    return cellImageName + "--" + getValidName(component.name) + "-service";
}

function getValidName(string name) returns string {
    return name.toLower().replace("_", "-").replace(".", "-");
}

public function setParameter(Env|Secret? param, string|int|boolean|float value) {
    if (param is (Env)) {
        param.value = value;
    } else if (param is Secret) {
        param.value = value;
    } else {
        error err = error("Parameter not declared in the component.");
        panic err;
    }
}
